// Spray tan reagent colors mobs, but not in a way that fucks it up too badly on our mutants.
/datum/reagent/spraytan/expose_mob(mob/living/exposed_mob, methods=TOUCH, reac_volume, show_message = TRUE)
	. = ..()
	if(ishuman(exposed_mob))
		if(methods & (PATCH|VAPOR))
			var/mob/living/carbon/human/exposed_human = exposed_mob
			if(HAS_TRAIT(exposed_human, TRAIT_USES_SKINTONES))
				switch(exposed_human.skin_tone)
					if("african1")
						exposed_human.add_atom_colour(skintone2hex("african2"), WASHABLE_COLOUR_PRIORITY)
					if("indian")
						exposed_human.add_atom_colour(skintone2hex("mixed2"), WASHABLE_COLOUR_PRIORITY)
					if("arab")
						exposed_human.add_atom_colour(skintone2hex("indian"), WASHABLE_COLOUR_PRIORITY)
					if("asian2")
						exposed_human.add_atom_colour(skintone2hex("arab"), WASHABLE_COLOUR_PRIORITY)
					if("asian1")
						exposed_human.add_atom_colour(skintone2hex("asian2"), WASHABLE_COLOUR_PRIORITY)
					if("mediterranean")
						exposed_human.add_atom_colour(skintone2hex("mixed1"), WASHABLE_COLOUR_PRIORITY)
					if("latino")
						exposed_human.add_atom_colour(skintone2hex("mediterranean"), WASHABLE_COLOUR_PRIORITY)
					if("caucasian3")
						exposed_human.add_atom_colour(skintone2hex("mediterranean"), WASHABLE_COLOUR_PRIORITY)
					if("caucasian2")
						exposed_human.add_atom_colour(skintone2hex("latino"), WASHABLE_COLOUR_PRIORITY)
					if("caucasian1")
						exposed_human.add_atom_colour(skintone2hex("caucasian2"), WASHABLE_COLOUR_PRIORITY)
					if("albino")
						exposed_human.add_atom_colour(skintone2hex("caucasian1"), WASHABLE_COLOUR_PRIORITY)
					if("mixed1")
						exposed_human.add_atom_colour(skintone2hex("mixed2"), WASHABLE_COLOUR_PRIORITY)
					if("mixed2")
						exposed_human.add_atom_colour(skintone2hex("mixed3"), WASHABLE_COLOUR_PRIORITY)
					if("mixed3")
						exposed_human.add_atom_colour(skintone2hex("african1"), WASHABLE_COLOUR_PRIORITY)
					if("mixed4")
						exposed_human.add_atom_colour(skintone2hex("mixed3"), WASHABLE_COLOUR_PRIORITY)
			//take current alien color and darken it slightly
			else if(HAS_TRAIT(exposed_human, TRAIT_MUTANT_COLORS) && !HAS_TRAIT(exposed_human, TRAIT_FIXED_MUTANT_COLORS))
				var/list/existing_color = rgb2num(exposed_human.dna.features["mcolor"])
				var/list/darkened_color = list()
				// Reduces each part of the color by 16
				for(var/channel in existing_color)
					darkened_color += max(channel - 17, 0)

				var/new_color = rgb(darkened_color[1], darkened_color[2], darkened_color[3])
				var/list/new_hsv = rgb2hsv(new_color)
				// Can't get too dark now
				if(new_hsv[3] >= 50)
					exposed_human.add_atom_colour(new_color, WASHABLE_COLOUR_PRIORITY)
			exposed_human.update_body(is_creating = TRUE)

		if((methods & INGEST) && show_message)
			to_chat(exposed_mob, span_notice("That tasted horrible."))

/datum/reagent/spraytan/overdose_process(mob/living/affected_mob, seconds_per_tick, times_fired)
	. = ..()
	metabolization_rate = 1 * REAGENTS_METABOLISM

	if(ishuman(affected_mob))
		var/mob/living/carbon/human/affected_human = affected_mob
		var/obj/item/bodypart/head/head = affected_human.get_bodypart(BODY_ZONE_HEAD)
		if(head)
			head.head_flags |= HEAD_HAIR //No hair? No problem!
		if(!HAS_TRAIT(affected_human, TRAIT_SHAVED))
			affected_human.set_facial_hairstyle("Shaved", update = FALSE)
		affected_human.set_facial_haircolor(COLOR_LIGHT_YELLOW, update = FALSE)
		if(!HAS_TRAIT(affected_human, TRAIT_BALD))
			affected_human.set_hairstyle("Spiky", update = FALSE)
		affected_human.set_haircolor(COLOR_LIGHT_YELLOW, update = FALSE)
		affected_human.add_atom_colour(COLOR_TAN_ORANGE, WASHABLE_COLOUR_PRIORITY)
		affected_human.update_body(is_creating = TRUE)
		if(SPT_PROB(3.5, seconds_per_tick))
			if(affected_human.w_uniform)
				affected_mob.visible_message(pick("<b>[affected_mob]</b>'s collar pops up without warning.</span>", "<b>[affected_mob]</b> flexes [affected_mob.p_their()] arms."))
			else
				affected_mob.visible_message("<b>[affected_mob]</b> flexes [affected_mob.p_their()] arms.")
	if(SPT_PROB(5, seconds_per_tick))
		affected_mob.say(pick("Shit was SO cash.", "You are everything bad in the world.", "What sports do you play, other than 'jack off to naked drawn Japanese people?'", "Don???t be a stranger. Just hit me with your best shot.", "My name is John and I hate every single one of you."), forced = /datum/reagent/spraytan)
